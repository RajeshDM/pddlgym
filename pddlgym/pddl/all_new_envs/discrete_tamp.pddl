(define (domain discrete_tamp)
  (:requirements :strips :equality :typing)
  (:types obj)
  ;(:constants q100)
  (:predicates
    (Conf ?q - obj)
    (Block ?b - obj)
    (Pose ?p - obj)
    (Kin ?q - obj ?p - obj)
    (AtPose ?b - obj ?p - obj)
    (AtConf ?q - obj)
    (Holding ?b - obj)
    (HandEmpty)
    (CFree ?p1 - obj ?p2 - obj)
    (Collision ?p1 - obj ?p2 - obj)
    (Unsafe ?p - obj)
    (CanMove)
  )
  (:action move
    :parameters (?q1 - obj ?q2 - obj)
    :precondition (and (Conf ?q1) (Conf ?q2)
                       (AtConf ?q1) (CanMove))
    :effect (and (AtConf ?q2) (not (CanMove))
                 (not (AtConf ?q1))
                 )
    ;:effect (and (AtConf ?q2)
    ;             (not (AtConf ?q1))
    ;             )
            
  )
  (:action pick
    :parameters (?b - obj ?p - obj ?q - obj)
    :precondition (and (Block ?b) (Kin ?q ?p)
                       (AtConf ?q) (AtPose ?b ?p) (HandEmpty))
    :effect (and (Holding ?b) (CanMove)
                 (not (AtPose ?b ?p)) (not (HandEmpty))
                 )
  )
  (:action place
    :parameters (?b - obj ?p - obj ?q - obj)
    :precondition (and (Block ?b) (Kin ?q ?p)
                       (AtConf ?q) (Holding ?b) (not (Unsafe ?p)))
    :effect (and (AtPose ?b ?p) (HandEmpty) (CanMove)
                 (not (Holding ?b))
                 )
  )
  (:derived (Unsafe ?p)
    (exists (?b2 - obj ?p2 - obj) (and (Pose ?p) (Block ?b2) (Pose ?p2)
                          (not (CFree ?p ?p2))
                          (AtPose ?b2 ?p2)))
  )
)
